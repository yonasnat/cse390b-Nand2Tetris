// File: projects/p6/JumpLogic.hdl
// This file is part of the CSE 390B
// project series, adapted from the
// Nand2Tetris project.

/**
 * Computes and outputs the load and inc signals
 * for the program counter based on the given instruction
 * and the given zr and ng signals output from the ALU.
 * The jump specification and table in chapter
 * 4.2.3 of the textbook will be very useful. 
 */
CHIP JumpLogic {
    IN  instruction[16], // current instruction
        aluzr,           // zr output from the ALU 
        alung;           // ng output from the ALU

    OUT load, // load signal to be used by the program counter
        inc;  // inc signal to be used by the program counter

    PARTS:
    Not(in=aluzr, out=notaluzr);
    Not(in=alung, out=notalung);
    And(a=notaluzr, b=alung, out=less);
    And(a=notaluzr, b=notalung, out=greater);
    And(a=less, b=instruction[2], out=jmpl);
    And(a=greater, b=instruction[0], out=jmpg);
    And(a=aluzr, b=instruction[1], out=jmpe);
    Or(a=jmpe, b=jmpl, out=out1);
    Or(a=jmpg, b=out1, out=jmp);
    And(a=jmp, b=instruction[15], out=jmp2, out=load);
    Not(in=jmp2, out=inc);
}